---
title: "Reduction of a proper ideal"
format: html
engine: markdown
---

## Definition

Let $S$ be a [numerical semigroup](../Basic-concepts/Numerical-semigroup.qmd) and $H,E$ two [proper ideals](Proper-ideal.qmd) of $S$ with $H \subseteq E$. It is said that $H$ is a reduction of $E$ if, for some $h \in \mathbb{N} \setminus \{0\}$, the equality $(h+1)E = H + hE$ holds, where $nE = \{e_1 + e_2 + \cdots + e_n ~ | ~ e_1, \ldots, e_n \in E\}$ for all $n \in \mathbb{N} \setminus \{0\}$.

It can be proven that $H$ is a reduction of $E$ if, and only if, $m(E) + S \subseteq H \subseteq E$, where $m(E)$ denotes the [multiplicity](Multiplicity-ideal.qmd) of $E$.

## Examples

$\circ$ Let $S$ a numerical semigroup and $E$ a proper ideal of $S$ with multiplicity $m(E)$. If $A - B = \{z \in \mathbb{Z} ~ | ~ z + B \subseteq A\}$ for any $A,B \subseteq \mathbb{Z}$, the following chain of relative ideals,

$$
S \subseteq E - m(E) \subseteq 2E - 2m(E) \subseteq \cdots \subseteq jE - jm(E) \subseteq \cdots \subseteq \mathbb{N},
$$

eventually stabilizes, since $|\mathbb{N} \setminus S|$ is finite. Then, there exists $h \in \mathbb{N} \setminus \{0\}$ such that $hE - hm(E) = (h+1)E - (h+1)m(E)$, or equivalently, $(h+1)E = hE + m(E) = [m(E) + S] + hE$. In conclusion, $m(E) + S$ is a reduction of $E$.

## Examples with GAP

Nowadays, there are no functions in package NumericalSgps related to reduction of proper ideals. However, given a numerical semigroup $S$ and two proper ideals $J,I$ of $S$, the following function returns true if $J$ is a reduction of $I$ and false otherwise.

``` gap
gap> IsReduction := function(S,J,I)
>       if not IsNumericalSemigroup(S) then
>               Error("First argument must be a Numerical Semigroup");
>       fi;
>       if not IsIdealOfNumericalSemigroup(J) then
>               Error("Second argument must be a relative ideal of the first argument");
>       fi;
>       if not AmbientNumericalSemigroupOfIdeal(J) = S then
>               Error("Second argument must be a relative ideal of the first argument");
>       fi;
>       if not IsIdealOfNumericalSemigroup(I) then
>               Error("Third argument must be a relative ideal of the first argument");
>       fi;
>       if not AmbientNumericalSemigroupOfIdeal(I) = S then
>               Error("Third argument must be a relative ideal of the first argument");
>       fi;
>       if not (IsIntegral(J) and IsIntegral(I)) then
>               return false;
>       fi;
>       if IsSubset(J, Minimum(I) + S) and IsSubset(I,J) then
>               return true;
>       fi;
>       return false;
> end;
function( S, J, I ) ... end
```

$\diamond$ Let $S = \langle 13, 15, 22, 27 \rangle$, $I = \{26, 28, 40, 41\} + S$ and $J = \{26, 41\} + S$, in GAP:

``` gap
gap> S := NumericalSemigroup(13, 15, 22, 27);
<Numerical semigroup with 4 generators>
gap> I := [26, 28, 40, 41] + S;
<Ideal of numerical semigroup>
gap> J := [26, 41] + S;
<Ideal of numerical semigroup>
```

Since $I,J \subseteq S$ are proper ideals and $m(I) + S \subseteq J \subseteq I$, then $J$ is a reduction of $I$.

``` gap
gap> IsReduction(S,J,I);
true
```

## References

<!-- https://docs.gap-system.org/doc/tut/chap0_mj.html -->
